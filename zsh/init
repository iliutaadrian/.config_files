# Nix
if [ -e '/nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh' ]; then
  . '/nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh'
fi

# Completions
autoload -Uz compinit

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'     # case-insensitive
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"  # colored output
zstyle ':completion:*' menu select                        # TAB cycles, Shift+TAB reverses
zstyle ':completion:*' special-dirs false                 # hide . and ..

compinit

# bash-completion (additional tool completions)
autoload -Uz bashcompinit && bashcompinit
[[ -r "/opt/homebrew/etc/profile.d/bash_completion.sh" ]] && source "/opt/homebrew/etc/profile.d/bash_completion.sh"

# direnv
command -v direnv &> /dev/null && eval "$(direnv hook zsh)"

# zoxide
command -v zoxide &> /dev/null && eval "$(zoxide init zsh)"

# fzf
if command -v fzf &> /dev/null; then
  if fzf --zsh > /dev/null 2>&1; then
    eval "$(fzf --zsh)"
  else
    # older fzf (pre-0.48) — source shell integration files directly
    for _fzf in \
      /usr/share/doc/fzf/examples/key-bindings.zsh \
      /usr/share/fzf/key-bindings.zsh; do
      [[ -f "$_fzf" ]] && source "$_fzf" && break
    done
    unset _fzf
  fi
fi

# Autosuggestions (history-based inline suggestions, → to accept)
for _zas in \
  /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh \
  /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh \
  /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh; do
  [[ -f "$_zas" ]] && source "$_zas" && break
done
unset _zas

# Starship prompt
command -v starship &> /dev/null && eval "$(starship init zsh)"

# zapmarks
if [[ -f "$HOME/.oh-my-zsh/custom/plugins/zapmarks/zapmarks.plugin.zsh" ]]; then
  source "$HOME/.oh-my-zsh/custom/plugins/zapmarks/zapmarks.plugin.zsh"
  bindkey '^Z' zapmarks
fi
